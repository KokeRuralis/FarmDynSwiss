********************************************************************************
$ontext

   FARMDYN project

   GAMS file : DEF_RUN.GMS

   @purpose  : Load include file from interface and define different
               globals used in the program
   @author   : W.Britz
   @date     : 24.09.14
   @since    :
   @refDoc   :
   @seeAlso  :
   @calledBy : exp_starter.gms

$offtext
********************************************************************************
*
* --- if no user supplied scratch directory used 22a.. directory from GAMS
*
$ifi not setglobal scrdir $setglobal scrdir %gams.scrdir%
*
* --- iScen is a flag from scen-gen or farm sample run
*     Set to blank if not supplied
*
$ifi not setglobal iScen  $setglobal iScen
*
* --- opt3/4/5 files used by solver will by replaced by a number
*     based on iScen. This allows for up to 10000 parallel threads
*
$iftheni.iScen not "%iScen%"==""
*
*  --- a non blank iScen implies parallel execution
*
   $$evalGlobal op3 round(%iScen%+10000)
   $$evalGlobal op4 round(%iScen%+20000)
   $$evalGlobal op5 round(%iScen%+30000)

   $$setglobal scenWithoutIncgen  %scen%
   $$set       scen  incgen/%scen%

$else.iScen
*
   $$setglobal op3 300
   $$setglobal op4 400
   $$setglobal op5 500
*
$endif.iScen
*
* --- overwrite default settings for random number generator
*     (to get a true random number and not always the same sequence)
*
$if setglobal seed option seed=%seed%;
*
* --- define the include file (passed by interface of calling program)
*
$if not set scen $setglobal scen incgen\runInc

  set dummyEmpty /""/;
*
* --- ensure that monhts are in correct order (in case these labels swhow up
*     in include file)
*
  set dummym "months in each year" / JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC /;

$onempty
 set selCrops(*);
*
* --- include file generated by interface (with potential add-ons from scen_gen)
*
$include '%scen%.gms'

$ifi not "%task%"=="Farm sample run" $ifi "%calibration%"=="true" $setglobal calibration file
$ifi     "%task%"=="Farm sample run" $ifi "%calibration%"=="true" $setglobal calibration sample
*
* --- include data for multiple farms in case of farm sample runs
*
$ifi "%task%"=="Farm sample run" $include 'solve/%buildSampleFile%.gms'
*
* --- if calibation is switched on, don't start from previous results
*
$ifi not "%calibration%"=="false" $setglobal startFromCalibRes false
*
* ---- set years if not supplied
*
$if not setglobal lastYear     $setglobal lastYear 2050
$if not setglobal firstYear    $setglobal firstYear 2003
$if not setglobal lastYearCalc $setglobal lastYearCalc 2050
*
* --- special settings for short run
*
$ifi "%dynamics%"=="Short run" $evalglobal lastYear     %firstYear%
$ifi "%dynamics%"=="Short run" $evalglobal lastYearCalc %lastYear%
*
* --- in comparative static mode, last year = first year
*
$ifi "%dynamics%"=="Comparative-static" $evalglobal lastYear     %firstYear%
$ifi "%dynamics%"=="Comparative-static" $evalglobal lastYearCalc %lastYear%
$ifi "%dynamics%"=="Comparative-static" $evalglobal stableYear   %firstYear%
$ifi "%dynamics%"=="Short run"          $evalglobal stableYear   %firstYear%
*
* ---- ensure that years are integers
*
$evalglobal firstYear   round(%firstYear%)
$evalglobal lastYear    round(%lastYear%)
$evalglobal lastOldYear %firstYear%-1
*
* --- year when an investment in a new stable must be done
*    (stableYear, from interface: year when the stable was build
*
$setglobal invHorizon 20
$evalGlobal invYear  (round(%stableYear%) + %invHorizon%)
*
* --- prolongcalc uses a weighted average of the last simulated years
*     to forecasts revenues/costs/machinery needs etc. until the years
*     the stables  are depreciated
*
$setglobal prolongCalc 0
$ifi "%dynamics%" == "Fully dynamic until stables are depreciated" $setglobal prolongCalc true
$ifi %prolongCalc% == true  $evalGlobal      prolongCalc (%invYear% <> %lastYear%)
*
 scalar p_prolongCalc / %prolongCalc% /;
*
$ifthen.prolong %prolongCalc% == 1
*
   $$evalGlobal prolongCalc (%invYear% < %lastYear%)
   $$evalGlobal lastYearCalc round(%invYear%)
   $$evalGlobal more30 (%lastYearCalc% < %lastYear%)

   $$ifi %more30%==1 $evalGlobal lastYearCalc (%invYear% + %invHorizon%)

$else.prolong

   $$evalGlobal lastYearCalc %lastYear%

$endif.prolong

$if not setglobal mode $setglobal mode %task%

$setlocal singleRun off
$ifi "%task%"=="Calculate MACs"  $setlocal singleRun on
$ifi "%task%"=="Single farm run" $setlocal singleRun on
$ifi "%task%"=="Farm sample run" $setlocal singleRun on
*
* --- standard in store_res.gm
*
$setglobal exploiterDim2 red0
*
* --- farmname is shown on the interface
*
$ifi "%task%"=="Farm sample run" $setglobal exploiterDim2 %farmIds%

$setglobal postFix
$iftheni.compstat not "%dynamics%" == "Comparative-static"
    $$setglobal postFix _until_%lastyear%
$endif.compStat


$iftheni.task "%singleRun%"=="on"
*
*  --- stand-alone results for a farm
*
  $$setglobal farmName %scenDes%%postFix%
  $$ifi "%task%"=="Farm sample run" $$setglobal farmName %scenDes%_%farmIds%%postFix%

  $$setglobal gdxFileName '%resdir%/expFarms/res_%farmName%.gdx'
  $$ifi "%task%"=="Farm sample run" $setglobal gdxFileName '%resdir%/samples/res_%farmName%.gdx'

$else.task
*
*  --- experiements from scen_gen
*
  $$setglobal farmName %scenWithoutIncgen%%postFix%
  $$setglobal gdxFileName '%resdir%/expFarms/res_%farmName%.gdx'

$endif.task


$setglobal PGMNAME %farmName%
*
$$setglobal MIP on
*
$include 'util/global_settings.gms'
$include 'util/title1.gms'
$include 'util/kill_model.gms'

$setglobal titlePrefix

$iftheni.iScen not "%iScen%"==""
*
*  --- if multiple farms run in parallel, don't
*      pump log into window
*
   $$batinclude 'util/title.gms' "'Suppress output'"
   $$setglobal titlePrefix console Draws:%iScen%
$endif.iScen
*
* --- branch specific settings
*
$setglobal dairyHerd false

$iftheni.dairy "%farmBranchDairy%" == "on"

  $$setglobal herd true
  $$setglobal dairyHerd true
  $$setglobal cowHerd true
  $$setglobal cattle true

$endif.dairy

$iftheni.mc "%farmBranchMotherCows%" == "on"

   $$ife %nMotherCows%==0 $abort "Mothercow branch is switched on, but herd size set to zero"

   $$setglobal herd true
   $$setglobal cowHerd true
   $$setglobal cattle true
   $$setglobal farmBranchBeef on
   $$setglobal sellHeifs true

$endif.mc

$iftheni.beef "%farmBranchBeef%" == "on"

  $$setglobal herd true
  $$setglobal cattle true
*
* -- if no cows are present, allow the beef farm
*    to buy young bulls
*
  $$iftheni.dc not "%farmBranchDairy%" == "on"
      $$iftheni.mc not "%farmBranchMotherCows%" == "on"
         $$setglobal buyYoungBulls true
      $$endif.mc
  $$endif.dc

$else.beef

  $$setglobal buyYoungBulls false

$endif.beef


$iftheni.sows "%farmBranchSows%" == "on"

  $$setglobal herd         true
  $$setglobal pigHerd      true

$endif.sows

$iftheni.fattners "%farmBranchFattners%" == "on"

  $$setglobal herd         true
  $$setglobal pigHerd      true

$endif.fattners

$ifi "%farmBranchArable%"    == "on" $setglobal arable true
$ifi "%farmBranchBiogas%"    == "on" $setglobal biogas true


* --- set manure global to true if biogas or herd, or if import of manure is allowed,
*     to include manure management

$if  not "%herd%" == true  $setglobal allowManureExport false
$ifi "%herd%"   == "true"            $setglobal manure true
$ifi "%biogas%" == "true"            $setglobal manure true
$ifi "%AllowManureImport%" == "true" $setglobal manure true

* --- Set manureStorage global if farm has herd or biogas plant

$ifi "%herd%" == "true"     $setglobal manureStorage    true
$ifi "%biogas%" == "true"   $setglobal manureStorage    true

* --- set strawManure global to activate straw manure management
*     (active if deep_litter is chosen for one of the cattle herd)

$setglobal strawManure false

$iftheni "%cattle%"=="true"
  $$ifi not "%heifersStableInv%"=="slatted_floor"   $setglobal strawManure true
  $$ifi not "%calvesStableInv%"=="slatted_floor"    $setglobal strawManure true
$endif
$iftheni "%farmBranchDairy%" == "on"
  $$ifi not "%cowStableInv%"=="slatted_floor"       $setglobal strawManure true
$endif
$iftheni "%farmBranchMotherCows%" == "on"
  $$ifi not "%motherCowStableInv%"=="slatted_floor" $setglobal strawManure true
$endif
$iftheni "%farmBranchBeef%" == "on"
  $$ifi not "%bullsStableInv%"=="slatted_floor"     $setglobal strawManure true
$endif

*
* --- steerings for GHG MAC cruns
*

$if not setGlobal redSteps $setGlobal redSteps 10
$if not setGlobal perCentRedPerStep $setGlobal perCentRedPerStep 2.5

$if not setglobal nMotherCows $setglobal nMotherCows 0

*
* --- standard rule is to use all CPU minus 1 as number of max threads
*     Might have been overwritten by interface settings threads
*
$if not setglobal threads $if not "%gams.threads%"=="1" $setglobal threads %gams.threads%
$if not setglobal threads $evalglobal threads %noCpu%-1
option threads=%threads%;


*
* --- Checks in crops datafile if KTBL_Data scalar is present to set a KTBL check for the large KTBL database
*
     $$onEmbeddedCode Python:
       def checkAuxDataSets(db):
         import os
         try:
             db.get_symbol('KTBL_Data')
             os.environ["database"] = "KTBL_database"
         except:
             os.environ["database"] = "user_database"
       rc = checkAuxDataSets(gams.ws.add_database_from_gdx(r'%datdir%/%cropsFile%.gdx'))
     $$offEmbeddedCode

$setglobal dataBase  %sysEnv.dataBase%
